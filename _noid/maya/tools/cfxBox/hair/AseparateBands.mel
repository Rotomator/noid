//=============================================================================
//
//	Separate Bands 		- Script that separates and renames poly BANDS
//						- Get the list of the initial selected TUBES/CURVES
//						- Creates a proxy of the combined BANDS
//						- Separates the proxy
//						- Rename each BAND with the name of it's intial TUBE/CURVE
//
//-----------------------------------------------------------------------------
//
// Version  :    1.1
//
// Created  :    18/08/2010
// Modified :    21/09/2010
//
// Creator 	:    Ali
// Contact 	:    chibi_ali@hotmail.com
//
//-----------------------------------------------------------------------------
//
// Use		:	- use "AseparateBands" to launch the script
//
//-----------------------------------------------------------------------------------
//===================================================================================
//***********************************************************************************








//***********************************************************************************
//===================================================================================
//--------------------------------     Main Procs    --------------------------------
//===================================================================================
//***********************************************************************************


//-------------------------------------------------------------------------
//
// Procedure that separates and wraps polyBANDS on corresponding nurbs TUBES/CURVES
//		<< takes for arg the combined polyBANDS mesh (that was generated by nurbs TUBES/CURVES)
//
//		>> returns the list of the separated bands
//
//-------------------------------------------------------------------------

global proc string[] AseparateBands()
{
	string $sel[];
	string $sourceBand;
	string $texWinName[];
	string $initSelOrderL;
	string $initSelOrder[];
	string $bandGroup;
	string $sourceBandPXY;
	string $combOrder[];
	string $newName;
	
	
	// ------------------------------------------------------------
	// -- Check that argument is a poly and has a "combineOrder" extraAttr
	//
	$sel = `filterExpand -fullPath false -sm 12 (ls ("-sl") )`;
	
	if (!`size($sel)`)
	{
		error ("Please select a POLY Band as source object !");
	}
	if (`size($sel)` > 1)
	{
		error ("Please select only ONE poly Band as source object !");
	}
	
	$sourceBand = $sel[0]; // get the transform even if $sourceBand was a shape
	
	if (!`attributeExists "combineOrder" $sourceBand`)
	{
		error ("The selected source object must have a \"combineOrder\" extra attribute !");
	}
	
	
	// ------------------------------------------------------------
	// -- Close UVTexture Editor if open (really really faster!)
	//
	$texWinName = `getPanel -sty polyTexturePlacementPanel`;
	// disable it if it's in embedded in a layout (like perspView)
	removeTextureWindow $texWinName[0];
	
	// close it if it's a separate window
	if ( `window -exists polyTexturePlacementPanel1Window` ) {
		deleteUI -window polyTexturePlacementPanel1Window;
	}
	
	
	// ------------------------------------------------------------
	// -- Get the list and order of the inital selected objects (the TUBES/CURVES that generated the bands)
	//
	$initSelOrderL = `getAttr ($sourceBand + ".combineOrder")`;
	$initSelOrder = stringToStringArray($initSelOrderL, " ");
	
	
	// ------------------------------------------------------------
	// -- Create a proxy of the polyBANDS and separate it
	//
	$bandGroup = "BandsSepar_GRP";
	
	if (!`objExists $bandGroup`) {
		$bandGroup = `createNode transform -n $bandGroup`;
	}
	
	$sourceBandPXY = `createNode transform -n ($sourceBand + "_GRP") -p $bandGroup`;
	createNode mesh -p $sourceBandPXY -n ($sourceBandPXY + "Shape");
	sets -e -forceElement initialShadingGroup $sourceBandPXY;
	connectAttr -f ($sourceBand + ".outMesh") ($sourceBandPXY + ".inMesh");
	
	$combOrder = `polySeparate -ch 0 $sourceBandPXY`;
	
	
	// ------------------------------------------------------------
	// -- Rename each polyBAND with the name of it's intial TUBE/CURVE
	//
	for ($i=0; $i<size($initSelOrder); $i++)
	{
		$newName = `substitute "^[^.]*:" $initSelOrder[$i] ""`;
		$newName = `substitute "nurbs" $newName "poly"`;
		$newName = `substitute "meche" $newName "bande"`;
		$combOrder[$i] = `rename $combOrder[$i] $newName`;
	}
	
	
	// ------------------------------------------------------------
	// -- Return the ordered list of the separated bands
	//
	select -cl;
	return $combOrder;
}

